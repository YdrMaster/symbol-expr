integer   = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
var_name  = @{ ("_" | ASCII_ALPHA) ~ ("_" | ASCII_ALPHANUMERIC)* }
list_name = @{ "&" ~ ("_" | ASCII_ALPHANUMERIC)+ }

unary_op = { "-" }
prod_op  = { "*" | "/" | "%" }
sum_op   = { "+" | "-" }

getter     = { list_name ~ "[" ~ algebra ~ "]" }
unit_expr  = { integer | var_name | getter | "(" ~ algebra ~ ")" }
unary_expr = { unary_op* ~ unit_expr }
prod_expr  = { unary_expr ~ (prod_op ~ unary_expr)* }
sum_expr   = { prod_expr ~ (sum_op ~ prod_expr)* }
algebra    = { sum_expr }

generator = { "[" ~ "|" ~ "i" ~ "|" ~ algebra ~ ";" ~ algebra ~ "]" }
array     = { "[" ~ algebra? ~ ("," ~ algebra)* ~ ","? ~ "]" }
list      = { generator | array }

WHITESPACE = _{ " " | NEWLINE }
